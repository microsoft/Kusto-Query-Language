---
title:  parse operator
description: Learn how to use the parse operator to parse the value of a string expression into one or more calculated columns.
ms.reviewer: alexans
ms.topic: reference
ms.date: 03/15/2023
---
# parse operator

Evaluates a string expression and parses its value into one or more calculated columns. The calculated columns will have nulls, for unsuccessfully parsed strings. If there's no need to use rows where parsing doesn't succeed, prefer using the [parse-where operator](parsewhereoperator.md).

## Syntax

*T* `| parse` [ `kind=`*kind* [ `flags=`*regexFlags* ]] *expression* `with` [ `*` ] *stringConstant* *columnName* [`:` *columnType*] [ `*` ] `,` ...

## Parameters

| Name | Type | Required | Description |
|--|--|--|--|
| *T* | string | &check; | The tabular input to parse.|
| *kind* | string | &check; | One of the [supported kind values](#supported-kind-values). The default value is `simple`.|
| *regexFlags* | string | |If *kind* is `regex`, then you can specify regex flags to be used like `U` for ungreedy, `m` for multi-line mode, `s` for match new line `\n`, and `i` for case-insensitive. More flags can be found in [RE2 flags](re2.md).|
| *expression* | string | &check; | An expression that evaluates to a string.|
| *stringConstant* | string | &check; | A string constant for which to search and parse.|
| *columnName* | string | &check; | The name of a column to assign a value to, extracted from the string expression. |
| *columnType* | string | | The scalar value that indicates the type to convert the value to. The default is `string`.|

> [!NOTE]
>
> * The parse pattern may start with *ColumnName* and not only with *StringConstant*.
> * Use `*` in the pattern to skip junk values. The `*` can't be used after a `string` type column.
> * If the parsed *expression* isn't of type `string`, it will be converted to type `string`.
> * Use [`project`](projectoperator.md) if you also want to drop or rename some columns.

### Supported kind values

|Text|Description|
|--|--|
| `simple` | This is the default value. *stringConstant* is a regular string value and the match is strict. All string delimiters should appear in the parsed string, and all extended columns must match the required types.|
| `regex` | *stringConstant* may be a regular expression and the match is strict. All string delimiters, which can be a regex for this mode, should appear in the parsed string, and all extended columns must match the required types.|
| `relaxed` | *stringConstant* is a regular string value and the match is relaxed. All string delimiters should appear in the parsed string, but extended columns may partially match the required types. Extended columns that didn't match the required types will get the value `null`.|

### Regex mode

In regex mode, parse will translate the pattern to a regex. Use [RE2 syntax](re2.md) to do the matching, and use numbered captured groups that are handled internally. For example:

```kusto
parse kind=regex Col with * <regex1> var1:string <regex2> var2:long
```

In the parse statement, the regex that will be internally generated by the parse is `.*?<regex1>(.*?)<regex2>(\-\d+)`.

* `*` was translated to `.*?`.

* `string` was translated to `.*?`.

* `long` was translated to `\-\d+`.

## Returns

The input table, extended according to the list of columns that are
provided to the operator.

## Examples

The `parse` operator provides a streamlined way to `extend` a table by using multiple `extract` applications on the same `string` expression. This result is useful, when the table has a `string` column that contains several values that you want to break into individual columns. For example, a column that was produced by a developer trace ("`printf`"/"`Console.WriteLine`") statement.

In the example below, assume that the column `EventText` of table `Traces` contains
strings of the form `Event: NotifySliceRelease (resourceName={0}, totalSlices={1}, sliceNumber={2}, lockTime={3}, releaseTime={4}, previousLockTime={5})`.
The operation will extend the table with six columns: `resourceName`, `totalSlices`, `sliceNumber`, `lockTime`, `releaseTime`, and `previousLockTime`.

> [!div class="nextstepaction"]
> <a href="https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA93UQU+DMBQA4DsJ/+Glp82QrHS6KYajN0OM42Y8MPbcqh2QtkxN/PG2oAyC27jsIhyg4b3Sr695AjXEMklRQQirRJt7KXB0t8NMx/ihA1Ba8mw9dh0w11P9INX3AKJc85fPheApPqLARCGMJKq8lClGyRbDB16g4Bku0g2uSoHSA53rRFQpKmRzD5R9jcrtEmXIph6IPH2LucmlbOLPJ4z6M6DXwSUNqO+BrH9zOKCQuON5qe7/nmZ6Y6LGxDuvw7866qCnHHSQg57dwej/qAdjhx3+KYc/pB6V9vznanbUcfxcDXaYc+U6z7eu89MYwHW+oEikWX3TF+Cd6w1cAOlwCLSHQLoa0h4FBpKt7QxtIGlz9yGNmTR8O3kbS9qjwLYyG0Jsdo9MervwmzC2CbVX5q+Y6g6ow+lUZl+Wzqr62/0N0O/KLnAFAAA=" target="_blank">Run the query</a>

```kusto
let Traces = datatable(EventText: string)
    [
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=23, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=15, lockTime=02/17/2016 08:40:00, releaseTime=02/17/2016 08:40:00, previousLockTime=02/17/2016 08:39:00)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=20, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=22, lockTime=02/17/2016 08:41:01, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=16, lockTime=02/17/2016 08:41:00, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:00)"
];
Traces  
| parse EventText with * "resourceName=" resourceName ", totalSlices=" totalSlices: long * "sliceNumber=" sliceNumber: long * "lockTime=" lockTime ", releaseTime=" releaseTime: date "," * "previousLockTime=" previousLockTime: date ")" *  
| project resourceName, totalSlices, sliceNumber, lockTime, releaseTime, previousLockTime
```

**Output**

|resourceName|totalSlices|sliceNumber|lockTime|releaseTime|previousLockTime|
|---|---|---|---|---|---|
|PipelineScheduler|27|15|02/17/2016 08:40:00|2016-02-17 08:40:00.0000000|2016-02-17 08:39:00.0000000|
|PipelineScheduler|27|23|02/17/2016 08:40:01|2016-02-17 08:40:01.0000000|2016-02-17 08:39:01.0000000|
|PipelineScheduler|27|20|02/17/2016 08:40:01|2016-02-17 08:40:01.0000000|2016-02-17 08:39:01.0000000|
|PipelineScheduler|27|16|02/17/2016 08:41:00|2016-02-17 08:41:00.0000000|2016-02-17 08:40:00.0000000|
|PipelineScheduler|27|22|02/17/2016 08:41:01|2016-02-17 08:41:00.0000000|2016-02-17 08:40:01.0000000|

### Regex mode

> [!div class="nextstepaction"]
> <a href="https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA9WUTU/CQBCG7036HyZ7arHItihoTYMevBlihJOUQ2lHWFlasrtFJP5414rYBvm4cDCbbDbtO+0878wORwV9EcUoIYAkUnqNOFr3C0xVH5fKl0qwdGybxsA0SPHYh26m2Mt7j7MYn5BjJBEsgTLLRYzdaIbBI5sjZyn24gkmOUfhgMpUxIsQGXhtB+TXsZvPRigCr+kAz+Jpn+lY6jXcdsOjbgvolX9Bfeo6IL5/s1swF7hgWS4f/v5M81qrbOKcjsG93MtADzHQoxjoSRk8+v/r4Hm7GdxDDO4xdShIT9tLrb0M+3vpaAbdS6YxvDGN9f0H0/iAeSR09lOWJnoeCBzjEjazAN6YmgCxzmsdexDVV3f152EtIFBGhVtSZQtlLUzO9K6jyoykTOzzLB0DEK2xfjK3OxsLyMaNQlKGJ2Urirdb5GTLDF8POi0OQ5usqUX2irGqkFRK8luPivfbPn8CDcwIyFAFAAA=" target="_blank">Run the query</a>

```kusto
let Traces = datatable(EventText: string)
    [
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=23, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=15, lockTime=02/17/2016 08:40:00, releaseTime=02/17/2016 08:40:00, previousLockTime=02/17/2016 08:39:00)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=20, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=22, lockTime=02/17/2016 08:41:01, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=16, lockTime=02/17/2016 08:41:00, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:00)"
];
Traces  
| parse kind = regex EventText with "(.*?)[a-zA-Z]*=" resourceName @", totalSlices=\s*\d+\s*.*?sliceNumber=" sliceNumber: long  ".*?(previous)?lockTime=" lockTime ".*?releaseTime=" releaseTime ".*?previousLockTime=" previousLockTime: date "\\)"  
| project resourceName, sliceNumber, lockTime, releaseTime, previousLockTime
```

**Output**

|resourceName|sliceNumber|lockTime|releaseTime|previousLockTime|
|---|---|---|---|---|
|PipelineScheduler|15|02/17/2016 08:40:00, |02/17/2016 08:40:00, |2016-02-17 08:39:00.0000000|
|PipelineScheduler|23|02/17/2016 08:40:01, |02/17/2016 08:40:01, |2016-02-17 08:39:01.0000000|
|PipelineScheduler|20|02/17/2016 08:40:01, |02/17/2016 08:40:01, |2016-02-17 08:39:01.0000000|
|PipelineScheduler|16|02/17/2016 08:41:00, |02/17/2016 08:41:00, |2016-02-17 08:40:00.0000000|
|PipelineScheduler|22|02/17/2016 08:41:01, |02/17/2016 08:41:00, |2016-02-17 08:40:01.0000000|

### Regex mode with regex flags

If you're interested in getting the resourceName only, use this query:

> [!div class="nextstepaction"]
> <a href="https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA92Uz0vDMBTH74X+D49e9oPAkkw3rfToTYq43sRDlj63uKwtSTon+McbWxAHruulF5NDXng/kg/vy9PoIDNCooUEcuH8Xmsc3x+wcBkeXQzWGVVsJmEAfj23R9T4Y0hLp14/VlpJfEKNwiKMDdqyNhJTscfkUVWoVYErucW81mgIuNIJ3aTYhC8J2G8zrfdrNAmfE9Cl3GXK51I+Y8sZp2wB9Ca+ojFlBEz7zPmAyuBBlbV9+LvM/NZHTSIyLAe77uSglzhoLw46OAen/6MfnJ/nYJc4WJ9+NLTD62rRydGtq94cXldh8HIXBu1gCINPqITxf9+pIvdjwuAGj/AzIuBduS1MITohi+D3FUZkBNOmkinfULoT7xfmHnmlhQQAAA==" target="_blank">Run the query</a>

```kusto
let Traces = datatable(EventText: string)
    [
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=23, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=15, lockTime=02/17/2016 08:40:00, releaseTime=02/17/2016 08:40:00, previousLockTime=02/17/2016 08:39:00)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=20, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=22, lockTime=02/17/2016 08:41:01, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=16, lockTime=02/17/2016 08:41:00, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:00)"
];
Traces
| parse kind = regex EventText with * "resourceName=" resourceName ',' *
| project resourceName
```

**Output**

|resourceName|
|---|
|PipelineScheduler, totalSlices=27, sliceNumber=23, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01|
|PipelineScheduler, totalSlices=27, sliceNumber=15, lockTime=02/17/2016 08:40:00, releaseTime=02/17/2016 08:40:00|
|PipelineScheduler, totalSlices=27, sliceNumber=20, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01|
|PipelineScheduler, totalSlices=27, sliceNumber=22, lockTime=02/17/2016 08:41:01, releaseTime=02/17/2016 08:41:00|
|PipelineScheduler, totalSlices=27, sliceNumber=16, lockTime=02/17/2016 08:41:00, releaseTime=02/17/2016 08:41:00|

You won't get the expected results, since the default mode is greedy.
If you have a few records where the `resourceName`  sometimes appears as lower-case and sometimes as upper-case, you may get nulls for some values.

To get the wanted result, run the query with the non-greedy `U`, and disable case-sensitive `i` regex flags.

> [!div class="nextstepaction"]
> <a href="https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA92UTUvDMBjH74V+h4de9kJhaeY2rfQg0ptWWbuTeMjaZ11c1pYknRP88GYtiIJ7ufRickjgeUl+PH/+AjUkkqWoIICMabOXAvvhDgud4F77oLTkRT6wLTDrpT2cJu5DVGq++ogFT3GOAplC6EtUZS1TjNgWg2deoeAFxukas1qgdEGXmommRAV05oI6XKN6u0QZ0LELokw3CTe1hI682YgSbwrk2r8iPvFckO0zxxMqiTte1urh7zbjG5M1cNxuObzJSQ5yjoNcxEE656Dkf8yD0uMc3jkO75J5NLTd62p6kuO0ri7mMLqyrddb22qNwbY+oWLS/H3Di8zYhMQc97ASLD+YxoLDt1vAO9drGIIzD+Onxfw+jO4ew8CBn8zQc3swbJrK8g1T/Sv6BX2O9haQBAAA" target="_blank">Run the query</a>

```kusto
let Traces = datatable(EventText: string)
    [
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=23, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=15, lockTime=02/17/2016 08:40:00, releaseTime=02/17/2016 08:40:00, previousLockTime=02/17/2016 08:39:00)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=20, lockTime=02/17/2016 08:40:01, releaseTime=02/17/2016 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=22, lockTime=02/17/2016 08:41:01, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=16, lockTime=02/17/2016 08:41:00, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:00)"
];
Traces
| parse kind = regex flags = Ui EventText with * "RESOURCENAME=" resourceName ',' *
| project resourceName
```

**Output**

|resourceName|
|---|
|PipelineScheduler|
|PipelineScheduler|
|PipelineScheduler|
|PipelineScheduler|
|PipelineScheduler|

If the parsed string has newlines, use the flag `s`, to parse the text.

> [!div class="nextstepaction"]
> <a href="https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA92UTVPCMBCG753pf9jJiTIqaVHQOh1P3hzGEW7WQygLRELCJCkfM/54Q5FaRT4uXEwO2U3eTfIkOyvQQk+zDA0kMGDW9b7A2uMcpe3h0sZgrOZyFPgeuPa6GUixHkNHWT5cdQXP8AUFMoNQ02hUrjPssCkmz3yGgkvsZmMc5AJ1Kq2yTBQhJonaqTRrs5NP+6iTqJlKobJJj7tYGjXCdiOiYQvobXxNYxqmUm+O2S+YaZxzlZunv7dp3jlVQC7OyxHeHOSgxzjoSRz07BwR/R//EUX7OcJjHOEp/1HQnj+vWgc5DufVyRwur3zv7d73NoXB9z5gxrS7+4TLQaJxhEsYCjYyiYGyUMCC2zHUgfzgI1B1t8UESO2q/hBUSQlUvBiEKlUlL4GtGa8rFVpnfWmq1OsjS29HuYNP4PdUNSZNAwLFA2j1jpm9ZAu2+ob+BOPecsY+BQAA" target="_blank">Run the query</a>

```kusto
let Traces = datatable(EventText: string)
    [
    "Event: NotifySliceRelease (resourceName=PipelineScheduler\ntotalSlices=27\nsliceNumber=23\nlockTime=02/17/2016 08:40:01\nreleaseTime=02/17/2016 08:40:01\npreviousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler\ntotalSlices=27\nsliceNumber=15\nlockTime=02/17/2016 08:40:00\nreleaseTime=02/17/2016 08:40:00\npreviousLockTime=02/17/2016 08:39:00)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler\ntotalSlices=27\nsliceNumber=20\nlockTime=02/17/2016 08:40:01\nreleaseTime=02/17/2016 08:40:01\npreviousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler\ntotalSlices=27\nsliceNumber=22\nlockTime=02/17/2016 08:41:01\nreleaseTime=02/17/2016 08:41:00\npreviousLockTime=02/17/2016 08:40:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler\ntotalSlices=27\nsliceNumber=16\nlockTime=02/17/2016 08:41:00\nreleaseTime=02/17/2016 08:41:00\npreviousLockTime=02/17/2016 08:40:00)"
];
Traces
| parse kind=regex flags=s EventText with * "resourceName=" resourceName: string "(.*?)totalSlices=" totalSlices: long "(.*?)lockTime=" lockTime: datetime "(.*?)releaseTime=" releaseTime: datetime "(.*?)previousLockTime=" previousLockTime: datetime "\\)" 
| project-away EventText
```

**Output**

|resourceName|totalSlices|lockTime|releaseTime|previousLockTime|
|---|---|---|---|---|
|PipelineScheduler<br>|27|2016-02-17 08:40:00.0000000|2016-02-17 08:40:00.0000000|2016-02-17 08:39:00.0000000|
|PipelineScheduler<br>|27|2016-02-17 08:40:01.0000000|2016-02-17 08:40:01.0000000|2016-02-17 08:39:01.0000000|
|PipelineScheduler<br>|27|2016-02-17 08:40:01.0000000|2016-02-17 08:40:01.0000000|2016-02-17 08:39:01.0000000|
|PipelineScheduler<br>|27|2016-02-17 08:41:00.0000000|2016-02-17 08:41:00.0000000|2016-02-17 08:40:00.0000000|
|PipelineScheduler<br>|27|2016-02-17 08:41:01.0000000|2016-02-17 08:41:00.0000000|2016-02-17 08:40:01.0000000|

### Relaxed mode

In this example for relaxed mode, `totalSlices` extended column must be of type `long`. However, in the parsed string, it has the value `nonValidLongValue`.
In `releaseTime` extended column, the value `nonValidDateTime` can't be parsed as `datetime`.
These two extended columns will get the value `null` while the other ones, such as `sliceNumber`, still get the correct values.

If you use option `kind = simple` for the same query below, you'll get `null` for all extended columns. This option is strict on extended columns, and is the difference between relaxed and simple mode.

> [!NOTE]
> In relaxed mode, extended columns can be partially matched.

> [!div class="nextstepaction"]
> <a href="https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA92Uz0/CMBTH70v2P7z0BGaGbijozG56I8QI8WI8lO0JldKStuNH4h9vNwIMp+gBPbge1rd9v6/99DVPoIWhZikaSCBj1o2RwMbdAqUd4srGYKzmctz0PXDP0+ZFyv8x9JXlL+uB4Ck+oEBmEBoajcp1in02w+Sez1FwiYN0glkuUAdglWWitJgk6gZgimk/n41QJ1E7AKHS6ZA7L41aYbcV0bAD9Cq+oDENA9CbZUqBVPKRCZ7dMlt+qMjmGhdc5ab3ebL2tVM1SfC7NOHlURp6nOZHEPTkENtt9JQcu0mOHypE/1OFouhrmrBGUxfQbzlK2r8uUtg5ikVPg+Xunu893/jepn/43hvMmXYoUy6zxK3AVpjBrpPAktsJnAE5ACVQDYEccpJqFDsmOS4yVFlJlXwv2eGT3UkUyavcpBrFRfMrJKRw1+hJ7UC2hqYzlOhavWJqz9mSrffQ7xu15UlfBQAA" target="_blank">Run the query</a>

```kusto
let Traces = datatable(EventText: string)
    [
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=23, lockTime=02/17/2016 08:40:01, releaseTime=nonValidDateTime 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=15, lockTime=02/17/2016 08:40:00, releaseTime=nonValidDateTime, previousLockTime=02/17/2016 08:39:00)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=nonValidLongValue, sliceNumber=20, lockTime=02/17/2016 08:40:01, releaseTime=nonValidDateTime 08:40:01, previousLockTime=02/17/2016 08:39:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=27, sliceNumber=22, lockTime=02/17/2016 08:41:01, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:01)",
    "Event: NotifySliceRelease (resourceName=PipelineScheduler, totalSlices=nonValidLongValue, sliceNumber=16, lockTime=02/17/2016 08:41:00, releaseTime=02/17/2016 08:41:00, previousLockTime=02/17/2016 08:40:00)"
];
Traces
| parse kind=relaxed EventText with * "resourceName=" resourceName ", totalSlices=" totalSlices: long * "sliceNumber=" sliceNumber: long * "lockTime=" lockTime ", releaseTime=" releaseTime: date "," * "previousLockTime=" previousLockTime: date ")" *
| project-away EventText
```

**Output**

|resourceName|totalSlices|sliceNumber|lockTime|releaseTime|previousLockTime|
|---|---|---|---|---|---|
|PipelineScheduler|27|15|02/17/2016 08:40:00||2016-02-17 08:39:00.0000000|
|PipelineScheduler|27|23|02/17/2016 08:40:01||2016-02-17 08:39:01.0000000|
|PipelineScheduler||20|02/17/2016 08:40:01||2016-02-17 08:39:01.0000000|
|PipelineScheduler||16|02/17/2016 08:41:00|2016-02-17 08:41:00.0000000|2016-02-17 08:40:00.0000000|
|PipelineScheduler|27|22|02/17/2016 08:41:01|2016-02-17 08:41:00.0000000|2016-02-17 08:40:01.0000000|
